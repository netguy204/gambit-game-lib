(load "object")
(load "rect")
(load "common")

(define (spatial-calc-idx width x y)
  (+ x (* y width)))

(define (spatial-idx this x y)
  (spatial-calc-idx (obj-field this 'width) x y))

(define (spatial-rect->idxs this rect)
  (let ((width (obj-field this 'width))
        (minx (floor (rect-minx rect)))
        (maxx (ceiling (rect-maxx rect)))
        (miny (floor (rect-miny rect)))
        (maxy (ceiling (rect-maxy rect)))
        (result '()))
    (let loopy ((y miny))
      (if (< y maxy)
          (let loopx ((x minx))
            (if (< x maxx)
                (begin
                  (set! result (cons (spatial-calc-idx width x y) result))
                  (loopx (+ x 1)))
                (loopy (+ y 1))))
          result))))

(define (spatial-append-unique-pred pred new old)
  (let loop ((new new)
             (old old))
    (if (null? new)
        old
        (if (some? (lambda (x) (pred x (car new))) old)
            (loop (cdr new) old)
            (loop (cdr new) (cons (car new) old))))))

(define (spatial-rect this rect)
  (let ((idxs (spatial-rect->idxs this rect))
        (idx->objs (obj-field this 'idx->objs)))

    (let loop ((idxs idxs)
               (result '()))
      (if (null? idxs)
          result
          (loop (cdr idxs)
                (spatial-append-unique-pred eq?
                  (table-ref idx->objs (car idxs) '())
                  result))))))

(define (spatial-delete! this value)
  (let* ((obj->idxs (obj-field this 'obj->idxs))
         (idx->objs (obj-field this 'idx->objs))
         (idxs (table-ref obj->idxs value '())))
    (let loop ((idxs idxs))
      (if (not (null? idxs))
          ;; remove this object from the index table
          (let* ((objs (table-ref idx->objs (car idxs)))
                 (objs (remove-if (lambda (o) (eq? o value)) objs)))
            (if (null? objs)
                (table-set! idx->objs (car idxs))
                (table-set! idx->objs (car idxs) objs))
            (loop (cdr idxs)))))
    ;; remove this object from the object table
    (table-set! obj->idxs value)))

(define (spatial-update! this value rect)
  (spatial-delete! this value)
  (let ((idxs (spatial-rect->idxs this rect))
        (obj->idxs (obj-field this 'obj->idxs))
        (idx->objs (obj-field this 'idx->objs)))
    (table-set! obj->idxs value idxs)
    (let loop ((idxs idxs))
      (if (not (null? idxs))
          (let ((values (table-ref idx->objs (car idxs) '())))
            (table-set! idx->objs (car idxs) (cons value values))
            (loop (cdr idxs))))))
  value)

(define (spatial-objects this)
  (map car (table->list (obj-field this 'obj->idxs))))

(define (spatial-make width)
  (obj-make fields: (list 'obj->idxs (make-table test: eq?)
                          'idx->objs (make-table)
                          'width width)))
